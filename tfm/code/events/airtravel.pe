#################################################
#                                               #
# Systems for air travel between the three maps #
#                                               #
#################################################

int airtravel_destmap = 0

local
int oldmap
const JETDEST_BASE = 11000
const PLAYERDEST_BASE = 11100
endlocal


##
##  Schedule function to move the local aircraft to the runway
##

function airtravel_movetorunway
activity
object target

# Have they brought down the forcefield?
if_not_user_flag "airtravel_on"
	return
endif

# Is the player watching?
if_onscreen me
	return
endif

find_tag target = "target" me.tag

if_not_exists target
	bug start
	print "aircraft runway tag "
	print me.tag
	print " not found!"
	printcr
	bug end
	return
endif

if me.x = target.x
and me.y = target.y
	# Already there
	stop_action me
	return
endif

transfer_object me to target
force_direction me faces target.curdir
end


##
##  Schedule function to move the itinerant aircraft back to their hiding places
##

function airtravel_hidevisitor
activity
object target
int tag

# Not while the player is onscreen
if_onscreen me
	return
endif

# Make sure they're out of range
get_distance tag = me player
if tag < 16
	return
endif

let tag = 0 - me.tag
find_tag target = "target" tag

if_not_exists target
	bug start
	print "aircraft hideaway tag "
	print tag
	print " not found!"
	printcr
	bug end
	return
endif

if me.x = target.x
and me.y = target.y
	# Already there
	stop_action me
	return
endif


play_sound "flyover"

transfer_object me to target

end


##
##  move player to map
##

function airtravel_map1
private
let airtravel_destmap = 1
call airtravel_move
end

function airtravel_map2
private
let airtravel_destmap = 2
call airtravel_move
end

function airtravel_map3
private
let airtravel_destmap = 3
call airtravel_move
end

##
##  Actually do it
##

function airtravel_move
private
int tag

get_mapno oldmap
let tag = PLAYERDEST_BASE + oldmap

play_sound "flyover"
fade_out

change_map airtravel_destmap at tag

# We rely on the sys_changemap callback to call airtravel_callback
# And fade the screen in again, etc

end

##
##  Move the jet into position and fade in
##
##  This is called by the engine via sys_changemap
##

function airtravel_callback
int tag
object jet
object target
string_array jetname[3]
string jet_name

if airtravel_destmap = 0
	return
endif

let airtravel_destmap = 0


let jetname[1] = "GR1-BLUE"
let jetname[2] = "F16-BLACK"
let jetname[3] = "F15-GREY"

let tag = JETDEST_BASE + oldmap

find_tag target = "target" tag
if_not_exists target
	bug start
	print "aircraft dest tag "
	print tag
	print " not found!"
	printcr
	bug end
	fade_in
	return
endif

let jet_name = jetname[oldmap]
find_tag jet = jet_name tag
if_not_exists jet
	bug start
	print "aircraft "
	print jet_name
	print " dest tag "
	print tag
	print " not found!"
	printcr
	bug end
	fade_in
	return
endif

transfer_object jet to target
force_direction jet faces target.curdir
fade_in

end

##
##  Activate it all (currently by cheating)
##

function enable_airtravel

set_user_flag "airtravel_on" = 1

end
