###################################
#                                 #
#  Move or rotate current object  #
#                                 #
###################################


##
##	Rotate left
##

function rotate_l

if_not_exists current
	let current = me
	if_not_exists current
		return
	endif
endif

if current.curdir = UP
	set_direction current = LEFT
	return
endif

if current.curdir = LEFT
	set_direction current = DOWN
	return
endif

if current.curdir = DOWN
	set_direction current = RIGHT
	return
endif

if current.curdir = RIGHT
	set_direction current = UP
	return
endif

end

##
##	Rotate right
##

function rotate_r

if_not_exists current
	let current = me
	if_not_exists current
		return
	endif
endif

if current.curdir = UP
	set_direction current = RIGHT
	return
endif

if current.curdir = RIGHT
	set_direction current = DOWN
	return
endif

if current.curdir = DOWN
	set_direction current = LEFT
	return
endif

if current.curdir = LEFT
	set_direction current = UP
	return
endif

end


##
##	Rotate 180 degrees
##

function flip_direction

if_not_exists current
	let current = me
	if_not_exists current
		return
	endif
endif

if current.curdir = UP
	set_direction current = DOWN
	return
endif

if current.curdir = DOWN
	set_direction current = UP
	return
endif

if current.curdir = LEFT
	set_direction current = RIGHT
	return
endif

if current.curdir = RIGHT
	set_direction current = LEFT
	return
endif

end



##
##	Move Forward
##

function move_forward

int x
int y
int cost

if_not_exists current
	return
endif

let x = current.x
let y = current.y

centre around current

if current.curdir = RIGHT
	add x + 1
	goto do_move
endif

if current.curdir = LEFT
	add x - 1
	goto do_move
endif

if current.curdir = DOWN
	add y + 1
	goto do_move
endif

if current.curdir = UP
	add y - 1
endif

label do_move

centre around current
get_cost cost = x y for current
if cost < 100
and cost >= 0
	move_object current to x y
endif


end



##
##	Track an enemy
##

function track_enemy

int x
int y
int ex
int ey
int cost

if_not_exists current
	return
endif

if_not_exists current.enemy
	return
endif

let x = current.x
let y = current.y
let ex = current.enemy.x
let ey = current.enemy.y

if x < ex
	set_direction current facing RIGHT
	add x + 1
endif

if x > ex
	set_direction current facing LEFT
	add x - 1
endif

if y < ey
	set_direction current facing DOWN
	add y + 1
endif

if y > ey
	set_direction current facing UP
	add y - 1
endif

centre around current
get_cost cost = x y for current
if cost < 100
	move_object current to x y
endif

end

function move_forward_action
activity
let current = me
call move_forward
end


##
##	Face your target
##

function face_target
integer xdx
integer ydy
integer abx
integer aby

if_not_exists current
	return
endif
if_not_exists current.target
	return
endif

let xdx = current.target.x - current.x
let ydy = current.target.y - current.y

# Get absolute X value
if xdx < 0
	let abx = 0 - xdx
else
	let abx = xdx
endif

# Get absolute Y value
if ydy < 0
	let aby = 0 - ydy
else
	let aby = ydy
endif


# Now, which is bigger, absolute X or absolute Y?

if abx > aby
	# absolute X is bigger, but face L or R?
	if xdx > 0
		set_direction current faces RIGHT
	else
		set_direction current faces LEFT
	endif
else
	# absolute Y is bigger, but face U or D?
	if ydy > 0
		set_direction current faces DOWN
	else
		set_direction current faces UP
	endif
endif

end
